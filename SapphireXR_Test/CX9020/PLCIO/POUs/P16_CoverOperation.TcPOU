<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="P16_CoverOperation" Id="{413bd09a-4287-4ca5-a610-3ab16d733b75}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM P16_CoverOperation
VAR_OUTPUT
	isCoverClosed 	: BOOL;
END_VAR	
VAR
	bCoverCP	: BOOL;
	bLowerLimit	: BOOL;
	bUpperLimit	: BOOL;
	TimerUpLED 		: TON;
	TimerUpLED_X 	: TON;
	TimerDownLED 	: TON;
	TimerDownLED_X 	: TON;
	TimerDownDelayOff 	: TON;
	(* Input *)
	bUpSwitch	: BOOL;
	bDownSwitch 	: BOOL;
	(* Output *)
	bUpLED 		: BOOL;
	bDownLED 	: BOOL;
	bOnUpRelay 	: BOOL;
	bOnDownRelay : BOOL;		
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Read from Device Value *)
// Cover Operation Switch
bUpSwitch 		:= GVL_IO.aDigitalInputIO[1].6;
bDownSwitch 	:= GVL_IO.aDigitalInputIO[1].7;
bUpperLimit 	:= GVL_IO.aDigitalInputIO[3].4;
bLowerLimit 	:= GVL_IO.aDigitalInputIO[3].5;
bCoverCP 		:=  GVL_IO.aDigitalInputIO[5].4;

TimerDownDelayOff(IN:=bLowerLimit, PT:=T#500MS);

// Cover Operation Condition
IF P90_Interlock.bCoverOperationEnable THEN
	bOnUpRelay := (bDownSwitch = FALSE AND bUpSwitch = TRUE AND bUpperLimit = FALSE);
	bOnDownRelay := (bDownSwitch = TRUE AND bUpSwitch = FALSE AND (bLowerLimit = FALSE OR TimerDownDelayOff.Q = FALSE ));
END_IF

//LED Blinking
TimerUpLED(IN:=(bOnUpRelay AND TimerUpLED_X.Q = FALSE), PT:=T#500MS);
TimerUpLED_X(IN:=TimerUpLED.Q, PT:=T#300MS);
bUpLED := (TimerUpLED.Q AND bOnUpRelay) OR bUpperLimit;

TimerDownLED(IN:=(bOnDownRelay AND TimerDownLED_X.Q = FALSE), PT:=T#500MS);
TimerDownLED_X(IN:=TimerDownLED.Q, PT:=T#300MS);
bDownLED := (TimerDownLED.Q AND bOnDownRelay) OR bLowerLimit;

//Status Output
isCoverClosed := bLowerLimit;

(* Write to Device Value *)
// Cover Operation Switch Lamp
GVL_IO.aDigitalOutputIO[4].2 := bUpLED;
GVL_IO.aDigitalOutputIO[4].3 := bDownLED;
GVL_IO.aDigitalOutputIO[1].6 := bOnUpRelay;
GVL_IO.aDigitalOutputIO[1].7 := bOnDownRelay;]]></ST>
    </Implementation>
    <LineIds Name="P16_CoverOperation">
      <LineId Id="29" Count="6" />
      <LineId Id="38" Count="25" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>